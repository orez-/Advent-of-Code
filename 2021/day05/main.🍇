💭 Point type
🕊🎯🍇
  🖍🆕x🔢
  🖍🆕y🔢

  🆕🍼x🔢 🍼y🔢🍇🍉
  🆕str🔡🍇
    🔫str 🔤,🔤❗️➡️ coords
    🍺🔢🐽coords 0❗ 10 ❗➡️🖍 x
    🍺🔢🐽coords 1❗️ 10 ❗️➡️🖍 y
  🍉

  ❓🚙➡️🔢🍇
    ↩️x
  🍉

  ❓🚀➡️🔢🍇
    ↩️y
  🍉

  💭 to_string-able
  🐊↘🔸🔡

  ❗️🔡➡️🔡🍇
    ↩️🔤🧲x🧲,🧲y🧲🔤
  🍉
🍉

💭 Dangerous cloud line type
🕊🚬🍇
  🖍🆕start🎯
  🖍🆕end🎯
  🖍🆕dx🔢
  🖍🆕dy🔢

  🆕 line 🔡 🍇
    🔫line 🔤 -> 🔤❗️➡️ coords
    🆕🎯🐽coords 0❗️❗️➡️🖍 start
    🆕🎯🐽coords 1❗️❗️➡️🖍 end
    ↪️ 🚙end❓ ▶️ 🚙start❓ 🍇
      1 ➡️🖍 dx
    🍉
    🙅↪️ 🚙end❓ 🙌 🚙start❓ 🍇
      0 ➡️🖍 dx
    🍉
    🙅 🍇
      -1 ➡️🖍 dx
    🍉
    ↪️ 🚀end❓ ▶️ 🚀start❓ 🍇
      1 ➡️🖍 dy
    🍉
    🙅↪️ 🚀end❓ 🙌 🚀start❓ 🍇
      0 ➡️🖍 dy
    🍉
    🙅 🍇
      -1 ➡️🖍 dy
    🍉
  🍉

  💭 Is this line axis-aligned?
  ❓📐➡️👌🍇
    ↩️🚙start❓🙌🚙end❓👐🚀start❓🙌🚀end❓
  🍉

  💭 What are the list of integer points this line crosses?
  ❓🚸➡️🍨🐚🎯🍆🍇
    🆕🍨🐚🎯🍆❗➡️🖍🆕 spots
    🚙start❓➡️🖍🆕x0
    🚙end❓➡️x1
    🚀start❓➡️🖍🆕y0
    🚀end❓➡️y1

    🔁 ❎🤜x0 🙌 x1 🤝 y0 🙌 y1🤛❗🍇
      🐻spots 🆕🎯x0 y0❗❗
      x0 ⬅️➕ dx
      y0 ⬅️➕ dy
    🍉
    🐻spots 🆕🎯x0 y0❗❗
    ↩️spots
  🍉

  💭 to_string-able
  🐊↘🔸🔡

  ❗️🔡➡️🔡🍇
    ↩️🔤🧲start🧲 -> 🧲end🧲🔤
  🍉
🍉

🏁 🍇
  🎞🐇💻❗️ ➡️ args
  🐽args 1❗ 🙌 🔤part2🔤 ➡️ part2?
  💭 dicts are string keys only ☹️☹️☹️
  🆕🍯🐚🔢🍆❗➡️🖍🆕crossings
  🆕🔡▶️👂🏼❗➡️🖍🆕 line
  🔁❎line🙌🔤🔤❗🍇
    🆕🚬line❗➡️ cloud
    ↪️part2? 👐 📐cloud❓🍇
      🔂spot 🚸cloud❓🍇
        🔡spot❗➡️key
        ↪️🐽crossings key❗➡️value🍇
          value➕1➡️🐽crossings key❗
        🍉
        🙅🍇
          1➡️🐽crossings key❗
        🍉
      🍉
    🍉
    🆕🔡▶️👂🏼❗➡️🖍 line
  🍉
  0➡️🖍🆕total
  🔂key 🐙crossings❗🍇
    ↪️🍺🐽crossings key❗▶️1🍇
      total ⬅️➕ 1
    🍉
  🍉
  😀🔡total❗❗
🍉
